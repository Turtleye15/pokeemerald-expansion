mapscripts Whitecliff_Castle_Infirmary_MapScripts {
    MAP_SCRIPT_ON_LOAD: CheckStarterBall
}

//VAR_Infirmary_Intro:
//0: Player in bed
//1: Player triggers Isabell walking in
//2: Player recieved starter

script CheckStarterBall{
    setflag(FLAG_STARTER_BALL)
}

script Infirmary_IsabellCheckPlayerRight{
    lock
    setvar(VAR_INFIRMARY_INTRO, 1)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceDown)
    goto(IsabellQuestions)
}


movement IsabellEnter{
    walk_up *4
    walk_right *2
    delay_16 *6
    walk_left*4
    face_up
}

script IsabellQuestions{
    applymovement(1, IsabellEnter)
    waitmovement(1)
    setvar(VAR_TEMP_2, 1)
    delay(60)
    addobject(2)
    clearflag(FLAG_STARTER_BALL)
    applymovement(1, IsabellWalkLeft)
    waitmovement(1)
    msgbox(HowAreYou)
    dynmultichoice(0,0, TRUE 2,0, DYN_MULTICHOICE_CB_NONE, "Could be better.","Awful...")
    msgbox(TellPlayer)
    dynmultichoice(0,0, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Golden Fields?","I'm not sure...","You tell me.")
    if(var(VAR_RESULT)==0){
        msgbox(GoldenFields)
    }
    elif(var(VAR_RESULT)==1){
        msgbox(NoIdea)
    }
    else{
        msgbox(Starving)
    }
    closemessage
    goto(AskPlayerName)
}
movement IsabellWalkLeft{
    walk_down *1
    face_left
    emote_exclamation_mark
    delay_16 * 5
    walk_left *3
    walk_up
}

movement FaceDown {
    face_down
}

text HowAreYou{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Ah, you're awake. \n Good.\p"
            "How are you feeling?")
}

text TellPlayer{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Wonderful, that means your condition has improved.\p You were on Yveltal's door when you were brought here.\p Apparantly you were out cold in the Golden Fields\p"
            "What happened to you?")
}

text GoldenFields{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Correct, they're right outside Whitecliff Castle.\p..\n..\l..\p You don't know where that is?")
}

text NoIdea{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Hmm, you don't know? Not surprising, I suppose. Can you recall anything else?")
}

text Starving{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}From what I could tell, you were starving to death. No signs of physical trauma.\p When'd you eat for the last time?")
}

script AskPlayerName{
    applymovement(OBJ_EVENT_ID_PLAYER, Question)
    waitmovement(0)
    applymovement(1, IsabellLookRight)
    waitmovement(1)
    msgbox(Amnesia)
    waitmessage
    msgbox(PlayerName)
    applymovement(OBJ_EVENT_ID_PLAYER, Exclamation)
    waitmovement(0)
    goto(AskPlayerMon)
}
script AskPlayerMon{ 
        dynmultichoice(0,0, TRUE 3,0, DYN_MULTICHOICE_CB_NONE, "I have a Roggenrola!", "It's a Litwick!","My Honedge!")
    if(var(VAR_RESULT)==0){
        bufferspeciesname(STR_VAR_3, SPECIES_ROGGENROLA)
        setvar(VAR_PICKED_STARTER, SPECIES_ROGGENROLA)
        }
    elif (var(VAR_RESULT)==1){
        bufferspeciesname(STR_VAR_3, SPECIES_LITWICK)
        setvar(VAR_PICKED_STARTER, SPECIES_LITWICK) 
        }
    else{
        bufferspeciesname(STR_VAR_3, SPECIES_HONEDGE)
        setvar(VAR_PICKED_STARTER, SPECIES_HONEDGE) 
    }
    goto(ConfirmPlayerMon)
}

script ConfirmPlayerMon{
    msgbox("{DESTROY_MUGSHOT}Are you sure?", MSGBOX_YESNO)
    if(var(VAR_RESULT)== NO){
        msgbox(AskAgain)
        goto(AskPlayerMon)
    }
    msgbox(Starter)
    closemessage
    applymovement(1, IsabellStepBackLeft)    
    waitmovement(1)
    release
}
text Amnesia{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}So... you really don't remember?\p Do you at least know who you are?")
}

text PlayerName{
    format("{PLAYER}... so you did not forget everything.\p How about your hometown? Friends? Family?\p..\n..\l..\pNothing at all... And what about your Pokemon? You had one with you.")
}

text AskAgain {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}So, then it was...?")
    
}

text Starter {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Ah, so you remember that as well.\p..\n How do I know?\p" 
            "Well, you weren't the only in critical condition when we found you.\p"
            "I've made sure your {STR_VAR_3} is healthy too.\p"
            "But, just to be sure, I need to test it's physical capabilities.\l" 
            "And yours, too.\p"
            "Your pokeball is on the table over there. Pick it up.")
}

movement Question {
    emote_question_mark
    delay_16*8
}

movement Exclamation {
    emote_exclamation_mark
    delay_16*4
}

movement IsabellLookRight {
    face_left
    delay_16*6
    face_up
}

movement IsabellStepBackLeft{
    walk_down
    walk_left * 3
    face_right
}

script IsabellBeforeStarterGet{
    lock
    faceplayer
    msgbox(NoPokemon)
    applymovement(1, IsabellFaceRight)
    waitmessage
    closemessage
    release
}

text NoPokemon{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Well, what are you waiting for? I don't have all day.")
}

movement IsabellFaceRight{
    face_right
}

script LeaveBeforeStarter{
    lock
    msgbox("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Don't try to sneak away!")
    closemessage
    removefieldmugshot
    applymovement(OBJ_EVENT_ID_PLAYER, GoBack)
    waitmovement(0)
    release
}

movement GoBack {
    walk_up
    
}

script ObtainStarterBall{
    showmonpic(VAR_PICKED_STARTER, 10, 3)
    bufferspeciesname(STR_VAR_3, VAR_PICKED_STARTER)
    msgbox("It's your {STR_VAR_3}!")
    waitmessage
    givemon(VAR_PICKED_STARTER, 5, ITEM_ORAN_BERRY)
    playfanfare(MUS_OBTAIN_ITEM)
    removeobject(2)
    setflag(FLAG_STARTER_BALL)
    setflag(FLAG_SYS_POKEMON_GET)
    setvar(VAR_INFIRMARY_INTRO, 2)
    call(NicknamePartyMon)
    hidemonpic
    closemessage
    goto(IsabellStarterBattle)
}

script NicknamePartyMon{
    msgbox("Did it have a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
}

script IsabellStarterBattle{
    applymovement(OBJ_EVENT_ID_PLAYER, Walkup)
    bufferspeciesname(STR_VAR_3, VAR_PICKED_STARTER)
    waitmovement(0)
    trainerbattle_single(TRAINER_ISABELL_1, Isabell_1_Intro, Isabell_1_Outro, IsabellBattleOver)
}

text Isabell_1_Intro {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Let's see if you still remember how to battle.")
}

text Isabell_1_Outro{
    format("Seems like you and your {STR_VAR_3} are doing great.")
}

movement Walkup {
    walk_down*1
    walk_left*1
    
}

script IsabellBattleOver{
    lockall
    msgbox(Healthy)
    release 
    end
}

text Healthy {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}It appears that you've both made a full recovery.\l Well, under my care that's to be expected\p"
            "Though, not even I can cure amnesia.")
}

script WC_Infirmary_PotionBox{
    lock
    msgbox(format("Glass bottles are piled up in this crate."))
    release
}

script WC_Infirmary_BerryTree{
    lock
    msgbox(format("A tree growing berries. It seems they will be used for making medicine."))
    release
}

script WC_Infirmary_GrassBox{
    lock
    msgbox(format("A box full with gilded leaves. Maybe they have medicinal qualities?"))
    release
}

script WC_Infirmary_Bookshelf{
    lock
    msgbox(format("There's a book here about potion brewing! Do you want to read it?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(GoldenFieldInfo)
    }
    if(var(VAR_RESULT) == NO){
        closemessage
        release
    }
    msgbox(format("Continue Reading?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(Brewing)
    }
    closemessage
    release
}

text GoldenFieldInfo{
    format("The Golden Fields surround Whitecliff Castle.\p" 
            "It's named after the grass that only grows in this region.\p"
            "For a cleric, these leaves serve an important purpose.\p"
            "They can draw out and amplify the properties of various berries.\p"
            "Most often, they are combined with Sitrus berries to create health potions.\p"
            "But, it works with lots of other berries!\p"
            "In the next chapters, I will explain this in more detail.")
}

text Brewing{
    format("Creating potions is surprisingly simple.\p"
            "You'll need berries, dried golden leaves, and an empty container\p"
            "First, crush the berries of choice lightly, and place them in the container.\l"
            "Then, add 1 leaf per berry\p"
            "Close the bottle tightly and after a little while your potion will be ready!\p"
            "While the process itself is simple, obtaining the desired result might be hard.\p"
            "You can add more ingredients to create potions with a wide range of effetcs!\p")
}

script InfirmaryMunna{
    lock 
    faceplayer
    playmoncry(SPECIES_MUNNA, CRY_MODE_ENCOUNTER)
    delay(20)
    msgbox(format("This Munna seems to be monitoring the patient!"))
    waitmessage
    applymovement(4, LookLeft)
    waitmovement(4)
    release
}

movement LookLeft {
    face_left
}