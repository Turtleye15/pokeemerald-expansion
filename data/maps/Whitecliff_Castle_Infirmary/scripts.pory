mapscripts Whitecliff_Castle_Infirmary_MapScripts {
    MAP_SCRIPT_ON_LOAD: Whitecliff_Infirmary_OnLoad
    // MAP_SCRIPT_ON_TRANSITION: Whitecliff_Infirmary_OnTransition
}

//VAR_CHAPTER_1:
//0: Player in bed
//1: Player triggers Isabell walking in
//2: Player recieved starter, recieved berry quest
//3: Player obtained berries
//4: Player handed over berries, started next day

const isabell = 1
const player = OBJ_EVENT_ID_PLAYER

script Whitecliff_Infirmary_OnLoad {
    if(var(VAR_CHAPTER_1) == 0){
        setflag(FLAG_STARTER_BALL)
        setobjectxyperm(isabell, 13,10)
        setobjectmovementtype(isabell, MOVEMENT_TYPE_FACE_DOWN)
    }
    elif(var(VAR_CHAPTER_1) == 1) {
        setobjectxyperm(isabell, 5,6)
    }
}


script Infirmary_IsabellEnterBuilding{
    lock
    setvar(VAR_CHAPTER_1, 1)
    applymovement(player, FaceDown)
    goto(IsabellQuestions)
}


movement IsabellEnter{
    walk_up *5
    walk_right *2
    delay_16 *3
    walk_left*4
    face_up
}

script IsabellQuestions{
    applymovement(isabell, IsabellEnter)
    waitmovement(isabell)
    setvar(VAR_TEMP_2, 1)
    delay(60)
    addobject(2)
    clearflag(FLAG_STARTER_BALL)
    applymovement(isabell, moves(walk_down *1,face_left))
    waitmovement(isabell)
    playse(SE_PIN)
    applymovement(isabell,IsabellWalkLeft)
    waitmovement(isabell)
    speakername("???")
    msgbox(HowAreYou)
    dynmultichoice(0,0, TRUE 2,0, DYN_MULTICHOICE_CB_NONE, "Could be better.","Awful...")
    msgbox(TellPlayer)
    dynmultichoice(0,0, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Golden Fields?","I'm not sure...","You tell me.")
    if(var(VAR_RESULT)==0){
        msgbox(GoldenFields)
    }
    elif(var(VAR_RESULT)==1){
        msgbox(NoIdea)
    }
    else{
        msgbox(Starving)
    }
    closemessage
    goto(AskPlayerName)
}
movement IsabellWalkLeft{
    emote_exclamation_mark
    delay_16 * 5
    walk_left *3
    walk_up
}

movement FaceDown {
    face_down
}

text HowAreYou{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Ah, you're awake. \n Good.\p"
            "How are you feeling?")
}

text TellPlayer{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Wonderful, that means your condition has improved.\p You were on Yveltal's door when you were brought here.\p Apparantly you were out cold in the Golden Fields\p"
            "What happened to you?")
}

text GoldenFields{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Correct, they're right outside Whitecliff Castle.\p..\n..\l..\p You don't know where that is?")
}

text NoIdea{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Hmm, you don't know? Not surprising, I suppose. Can you recall anything else?")
}

text Starving{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}From what I could tell, you were starving to death. No signs of physical trauma.\p When'd you eat for the last time?")
}

script AskPlayerName{
    playse(SE_PIN)
    applymovement(player, Question)
    waitmovement(0)
    applymovement(isabell, IsabellLookRight)
    waitmovement(isabell)
    speakername("???")
    msgbox(Amnesia)
    waitmessage
    applymovement(player,moves(walk_in_place_down))
    msgbox(PlayerName)
    playse(SE_PIN)
    applymovement(player, Exclamation)
    waitmovement(0)
    goto(AskPlayerMon)
}
script AskPlayerMon{ 
        dynmultichoice(0,0, TRUE 3,0, DYN_MULTICHOICE_CB_NONE, "My Roggenrola!", "It's a Litwick!","I have an Aron!")
    if(var(VAR_RESULT)==0){
        bufferspeciesname(STR_VAR_1, SPECIES_ROGGENROLA)
        setvar(VAR_PICKED_STARTER, SPECIES_ROGGENROLA)
        }
    elif (var(VAR_RESULT)==1){
        bufferspeciesname(STR_VAR_1, SPECIES_LITWICK)
        setvar(VAR_PICKED_STARTER, SPECIES_LITWICK) 
        }
    else{
        bufferspeciesname(STR_VAR_1, SPECIES_ARON)
        setvar(VAR_PICKED_STARTER, SPECIES_ARON)
    }
    goto(ConfirmPlayerMon)
}

script ConfirmPlayerMon{
    msgbox("{DESTROY_MUGSHOT}Are you sure?", MSGBOX_YESNO)
    if(var(VAR_RESULT)== NO){
        msgbox(AskAgain)
        goto(AskPlayerMon)
    }
        msgbox(Starter)
        closemessage
        applymovement(isabell, IsabellStepBackLeft)    
        waitmovement(isabell)
        speakername("Isabell")
        msgbox(MyNameIs)
        closemessage
        setobjectmovementtype(isabell, MOVEMENT_TYPE_FACE_RIGHT)
        closemessage
        release
}
text Amnesia{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}So... you really don't remember?\p Do you at least know who you are?")
}

text PlayerName{
    format("{PLAYER}... so you did not forget everything.\p How about your hometown? Friends? Family?\p..\n..\l..\pNothing at all... And what about your Pokemon? You had one with you.")
}

text AskAgain {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}So, then it was...?")
    
}

text Starter {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Ah, so you remember that as well.\p..\n How do I know?\p" 
            "Well, you weren't the only one in critical condition when we found you.\p"
            "I've made sure your {STR_VAR_1} is healthy too.\p"
            "But, just to be sure, I need to test it's physical capabilities.\l" 
            "And yours, too.\p"
            "Your pokeball is on the table over there. Pick it up.")
}

text MyNameIs {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}By the way, you may call me Isabell. I run this infirmary.")
    
}

movement Question {
    emote_question_mark
    delay_16*8
}

movement Exclamation {
    emote_exclamation_mark
    delay_16*4
}

movement IsabellLookRight {
    face_left
    delay_16*6
    face_up
}

movement IsabellStepBackLeft{
    walk_down
    walk_left * 3
    face_right
}



text NoPokemon{
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Well, what are you waiting for? I don't have all day.")
}

movement IsabellFaceRight{
    face_right
}

script LeaveBeforeStarter{
    lock
    speakername("Isabell")
    msgbox("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Don't try to sneak away!")
    closemessage
    removefieldmugshot
    applymovement(player, GoBack)
    waitmovement(0)
    release
}

movement GoBack {
    walk_up
    
}

script ObtainStarterBall{
    showmonpic(VAR_PICKED_STARTER, 10, 3)
    playmoncry(VAR_PICKED_STARTER,CRY_MODE_NORMAL)
    bufferspeciesname(STR_VAR_1, VAR_PICKED_STARTER)
    msgbox("It's your {STR_VAR_1}!")
    waitmessage
    givemon(VAR_PICKED_STARTER, 5, ITEM_ORAN_BERRY)
    playfanfare(MUS_OBTAIN_ITEM)
    removeobject(2)
    setflag(FLAG_STARTER_BALL)
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_SYS_POKEMON_GET)
    setflag(FLAG_SYS_B_DASH)
    setvar(VAR_CAN_HEAL, 1)
    setvar(VAR_CHAPTER_1, 2)
    call(NicknamePartyMon)
    hidemonpic
    closemessage
    goto(IsabellStarterBattle)
}

script NicknamePartyMon{
    msgbox("Did it have a nickname?", MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES){
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
}

script IsabellStarterBattle{
    applymovement(player, Walkup)
    bufferspeciesname(STR_VAR_1, VAR_PICKED_STARTER)
    waitmovement(0)
    trainerbattle_single(TRAINER_ISABELL_1, Isabell_1_Intro, Isabell_1_Outro, IsabellBattleOver)
}

text Isabell_1_Intro {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Let's see if you still remember how to battle.")
}

text Isabell_1_Outro{
    format("Seems like you and {STR_VAR_1} are doing great.")
}

movement Walkup {
    walk_down*1
    walk_left*1
    
}

script IsabellBattleOver{
    lockall
    bufferspeciesname(STR_VAR_1, VAR_PICKED_STARTER)
    speakername("Isabell")
    msgbox(WC_Infirmary_Healthy)
    closemessage
    applymovement(isabell, HealPlayer)
    waitmovement(isabell)
    special(HealPlayerParty)
    playfanfare(MUS_HEAL)
    delay(60)
    speakername("Isabell")
    msgbox(WC_Infirmary_WhatNow)
    dynmultichoice(0,0, TRUE 1,0, DYN_MULTICHOICE_CB_NONE, "Can't you do anything?")
    msgbox(WC_Infirmary_ThePast)
    waitmessage
    applymovement(isabell, LookUp)
    waitmovement(isabell)
    msgbox(WC_Infirmary_ThePresent)
    questmenu(QUEST_MENU_SET_ACTIVE, QUEST_CHAPTER_1)
    subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_CHAPTER_1, SUB_QUEST_1)
    closemessage
    release 
    end
}

text WC_Infirmary_Healthy {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}It appears that you've both made a full recovery.\l Well, under my care that's to be expected.\p"
            "Here, let me heal your {STR_VAR_1}.")
}

text WC_Infirmary_WhatNow {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}So, with that out of the way...\n What is your plan now? I can't imagine losing your memories is pleasant.\p"
           "But, it seems your amnesia is only partial - you still know how to communicate and command your pokemon.\p")
}

text WC_Infirmary_ThePast {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}I appreciate your sentiment, but not even I can cure amnesia.\p" 
           "I specialize in curing phsyical trauma, mental issues are outside my expertise.")
    
}

text WC_Infirmary_ThePresent {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Hmmm... I might know someone who can help you out. But, I have to pull some strings for that.\p"
            "You should stay here for a while to fully recover anyway.\p"
            "I almost ran out of Rawst & Sitrus berries taking care of you.\p"
            "So I'll need you to restock those for me at the farm.\p"
            "Some fresh air will do you well, and you can explore the castle if you want.\p"
            "The farm is just west of Whitecliff Castle. Tell them Isabell sent you.")
}

movement HealPlayer {
    walk_right * 4
}

movement Walkinplace {
    walk_in_place_left
}

movement LookUp {
    walk_left
    face_up
    delay_16 *4
    face_right
}

text IsabellGetBerries {
    format("{CREATE_MUGSHOT MUGSHOT_ISABELL EMOTE_NORMAL}Have you brought back the berries yet? If not, please don't bother me.")
}

script Infirmary_IsabellTalk{
    lock
    faceplayer
    switch (var(VAR_CHAPTER_1)){
        case 0:
        case 1:
            goto(IsabellBeforeStarterGet)
        case 2:
            goto(WC_Infirmary_GetBerries)
        case 3:
            goto(WC_Infirmary_DeliverBerries)
        }
    }


script IsabellBeforeStarterGet{
    speakername("Isabell")
    msgbox(NoPokemon)
    applymovement(isabell, IsabellFaceRight)
    waitmessage
    closemessage
    release
}

script WC_Infirmary_GetBerries {
    speakername("Isabell")
    msgbox(IsabellGetBerries)
    questmenu(QUEST_MENU_SET_ACTIVE, QUEST_CHAPTER_1)
    closemessage
    release    
}

script WC_Infirmary_DeliverBerries {
    removeitem(ITEM_BERRY_SATCHEL)
    setvar(VAR_CHAPTER_1,4)
    speakername("Isabell")
    msgbox(WC_Infirmary_BerryDelivery)
    waitmessage
    dynmultichoice(0,0, TRUE 3,0, DYN_MULTICHOICE_CB_NONE, "It was nice.","These cliffs are tall!","I ran into Diana.",)
    switch(var(VAR_RESULT)){
        case 0:
            msgbox(WC_Infirmary_Sightseeing)
        case 1:
            msgbox(WC_Infirmary_Altitude)
        case 2:
            msgbox(WC_Infirmary_Dania)
    
    }
    waitmessage
    msgbox("Allright, you should get some rest.\nI'll wake you up in a couple of\lhours.")
    removefieldmugshot
    fadescreen(FADE_TO_BLACK)
    warpsilent(MAP_WHITECLIFF_CASTLE_INFIRMARY,9,3)
    setflag(FLAG_ISABELL_LOCATION_INFIRMARY)
    removeobject(isabell)
    delay(80)
    fadescreen(FADE_FROM_BLACK)
    
}

text WC_Infirmary_BerryDelivery {
    "{CREATE_MUGSHOT MUGSHOT_DANIA EMOTE_NORMAL}There you are. Do you have\neverything I sent you to collect?\p"
    "⋯\n Wonderful. That's all for now.\p"
    "Did you enjoy the walk around the\ncastle?"
}

text WC_Infirmary_Sightseeing {
    "Mhm, this place is quite stunning.\nI love it up here."
}

text WC_Infirmary_Altitude {
    "Oh yeah, this place is incredibly\nhigh up.\p"
    "Makes it hard to reach. Especially\nwithout using the lift."
}

text WC_Infirmary_Dania {
    "Oh, you've met our cavalry captain?\nI assumed she'd already left for\lher next mission"
    "I should've expected she would stall\nthings out⋯ typical."
}

script WC_Infirmary_DayTwo {

    speakername("Isabell")
    msgbox("-ve nobody else who can help-\n⋯\l-ne, I'll get them-")
    waitmessage
    setobjectxyperm(isabell, 13,9)
    clearflag(FLAG_ISABELL_LOCATION_INFIRMARY)
    playse(SE_DOOR)
    addobject(isabell)
    applymovement(isabell, moves(walk_up*3, face_left))
    // speakername("Isabell")
    // msgbox("{CREATE_MUGSHOT MUGSHOT_DANIA EMOTE_NORMAL}Hey. I need your help")
    closemessage
    release
}


script RestAtInfirmary{
    lockall
    applymovement(player, FaceDown)
    msgbox(format("This bed is nice and confortable. Would you like to rest?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)== YES){
        goto(Common_Eventscript_Inn_Heal)
        msgbox(format("You and your pokemon feel refreshed!"))
        releaseall
        end
    }
    else{
        closemessage
        releaseall
        end
    }
}

script WC_Infirmary_PotionBox{
    lock
    msgbox(format("Glass bottles are piled up in this crate."))
    release
}

script WC_Infirmary_BerryTree{
    lock
    msgbox(format("A tree growing berries. It seems they will be used for making medicine."))
    release
}

script WC_Infirmary_GrassBox{
    lock
    msgbox(format("A box full with gilded leaves. Maybe they have medicinal qualities?"))
    release
}

script WC_Infirmary_Bookshelf{
    lock
    msgbox(format("There's a book here about potion brewing! Do you want to read it?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(GoldenFieldInfo)
    }
    if(var(VAR_RESULT) == NO){
        closemessage
        release
    }
    msgbox(format("Continue Reading?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(Brewing)
    }
    closemessage
    release
}

text GoldenFieldInfo{
    format("The Golden Fields surround Whitecliff Castle.\p" 
            "It's named after the grass that only grows in this region.\p"
            "For a cleric, these leaves serve an important purpose.\p"
            "They can draw out and amplify the properties of various berries.\p"
            "Most often, they are combined with Sitrus berries to create health potions.\p"
            "But, it works with lots of other berries!\p"
            "In the next chapters, I will explain this in more detail.")
}

text Brewing{
    format("Creating potions is surprisingly simple.\p"
            "You'll need berries, dried golden leaves, and an empty container\p"
            "First, crush the berries of choice lightly, and place them in the container.\l"
            "Then, add 1 leaf per berry\p"
            "Close the bottle tightly and after a little while your potion will be ready!\p"
            "While the process itself is simple, obtaining the desired result might be hard.\p"
            "You can add more ingredients to create potions with a wide range of effetcs!\p")
}

script InfirmaryMunna{
    lock 
    faceplayer
    playmoncry(SPECIES_MUNNA, CRY_MODE_ENCOUNTER)
    delay(20)
    msgbox(format("This Munna seems to be monitoring the patient!"))
    waitmessage
    applymovement(4, LookLeft)
    waitmovement(4)
    release
}

movement LookLeft {
    face_left
}